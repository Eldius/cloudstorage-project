group 'net.eldiosantos.cloudstorage'
version '0.0.1-SNAPSHOT'

apply plugin: 'java'
apply plugin: 'jacoco'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

test {
    testLogging {
        showStandardStreams = true
        showExceptions = true
        exceptionFormat = 'full'
        //events = ['standard_out', 'standard_error']
    }

    systemProperty 'storage.dropbox.url', 'wrong_url'

    options {
        systemProperty "storage.dropbox.contentUrl", "wrong_url_again"
    }
}


apply plugin: 'org.junit.platform.gradle.plugin'

ext.junit4Version = '4.12'
ext.junitVintageVersion = '4.12.0-M3'
ext.junitPlatformVersion = '1.0.0-M3'
ext.junitJupiterVersion = '5.0.0-M3'
ext.log4jVersion = '2.6.2'


compileTestJava {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
    options.compilerArgs += '-parameters'
}

junitPlatform {
    platformVersion '1.0.0-M3'
    filters {
        engines {
            include 'junit-jupiter', 'junit-vintage'
            // exclude 'custom-engine'
        }
        tags {
            // include 'fast'
            exclude 'slow'
        }
        // includeClassNamePattern '.*Test'
    }
    // enableStandardTestTask true
    reportsDir file('build/test-results/junit-platform') // this is the default
    logManager 'org.apache.logging.log4j.jul.LogManager'
}

tasks.whenTaskAdded { task ->
    if (task.name.equals('junitPlatformTest')) {
        System.out.println("ADDING TASK " + task.getName() + " to the project!")

        // configure jacoco to analyze the junitPlatformTest task
        jacoco {
            // this tool version is compatible with
            toolVersion = "0.7.6.201602180812"
            applyTo task
        }

        // create junit platform jacoco task
        project.task(type: JacocoReport, "junitPlatformJacocoReport",
                {
                    sourceDirectories = files("./src/main")
                    classDirectories = files("$buildDir/classes/main")
                    executionData task
                })
    }
}

afterEvaluate {
    def junitPlatformTestTask = tasks.getByName('junitPlatformTest')

    junitPlatformTestTask.systemProperty 'storage.dropbox.app-key', System.getenv("appkey")
    junitPlatformTestTask.systemProperty 'storage.dropbox.app-secret', System.getenv("appsecret")
    junitPlatformTestTask.systemProperty 'storage.dropbox.access-token', System.getenv("accesstoken")

    junitPlatformTestTask.finalizedBy(generateHtmlTestReports)
    //generateHtmlTestReports.dependsOn(junitPlatformTestTask)
    check.dependsOn(generateHtmlTestReports)

}

configurations {
    junitXmlToHtml
}

task generateHtmlTestReports << {
    def reportsDir = new File(buildDir, 'test-reports-ant')
    reportsDir.mkdirs()

    ant.taskdef(
            name: 'junitReport',
            classname: 'org.apache.tools.ant.taskdefs.optional.junit.XMLResultAggregator',
            classpath: configurations.junitXmlToHtml.asPath
    )

    ant.junitReport(todir: "$buildDir/test-results/junit-platform", tofile: "aggregated-test-results.xml") {
        fileset(dir: "$buildDir/test-results/junit-platform")
        report(format: 'frames', todir: reportsDir)
    }
}

dependencies {
    compile project(":cloudstorage-configuration")
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.21'

    testCompile group: 'org.slf4j', name: 'slf4j-log4j12', version: '1.7.21'

    // JUnit Jupiter API and TestEngine implementation
    testCompile("org.junit.jupiter:junit-jupiter-api:${junitJupiterVersion}")
    testRuntime("org.junit.jupiter:junit-jupiter-engine:${junitJupiterVersion}")

    // If you also want to support JUnit 3 and JUnit 4 tests
    testCompile("junit:junit:${junit4Version}")
    testRuntime("org.junit.vintage:junit-vintage-engine:${junitVintageVersion}")

    testRuntime("org.apache.logging.log4j:log4j-core:${log4jVersion}")
    testRuntime("org.apache.logging.log4j:log4j-jul:${log4jVersion}")

    // https://mvnrepository.com/artifact/info.cukes/cucumber-java8
    testCompile group: 'info.cukes', name: 'cucumber-java8', version: '1.2.5'
    // https://mvnrepository.com/artifact/info.cukes/cucumber-junit
    testCompile group: 'info.cukes', name: 'cucumber-junit', version: '1.2.5'
    // https://mvnrepository.com/artifact/com.google.truth/truth
    //testCompile group: 'com.google.truth', name: 'truth', version: '0.31'
    // https://mvnrepository.com/artifact/com.google.truth.extensions/truth-java8-extension
    //testCompile group: 'com.google.truth.extensions', name: 'truth-java8-extension', version: '0.31'

    junitXmlToHtml 'org.apache.ant:ant-junit:1.10.0'
}
